import org.ajoberstar.grgit.*

buildscript {
	repositories {
    	mavenCentral()
	}

	dependencies {
		classpath 'org.ajoberstar:gradle-git:0.12.0'
	}
}

plugins {
    id 'java'
	id 'idea'
	id 'eclipse'
}

repositories {
    mavenCentral()
}

dependencies {
    compile project (':../human-interface-devices')
	compile project (':../OSC')
    compile project (':../p5-Helpers')
    compile project (':../p5-Musical')
    compile project (':../p5-Rift')
    compile project (':../p5-Spatial')
    compile project (':../p5-VR')
    compile project (':../PS3Eye')
    compile project (':../processing/core')
	compile 'cz.advel.jbullet:jbullet:20101010-1'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7
version = '1.0'

idea {
	project {
	    languageLevel = '1.7'
	}
	module {
		name = 'OculusRiftSandbox'
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.2.1'
}


def cloneRepo(repo) {
	def cloneTo = file(repo.cloneTo)

	if(!cloneTo.exists()) {
		logger.quiet 'cloning into ' + cloneTo
		
		def grgit = Grgit.clone(dir: cloneTo, uri: repo.uri )
		
		if(repo.checkout) {
			logger.quiet 'checking out ' + repo.checkout
			grgit.checkout(branch: repo.checkout)
		}
	} else {
		logger.quiet 'already cloned ' + cloneTo
	}
}

task copyPS3EyeDlls(type: Copy) {
	from '../PS3Eye/dlls/'
	into 'dll/ps3Eye'
	include '*'
}

task setup << {
	gradle.gitDependencies.each() {
		cloneRepo it
	}

	tasks.copyPS3EyeDlls.execute()
}
